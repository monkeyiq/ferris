declare namespace libferris = "http://www.libferris.com"; 
declare function libferris:inet_aton 
  ( $ip as xs:string? )  as xs:string {
       
	let $toks := tokenize($ip,'\.')
	return
	(
	(($toks[1] cast as xs:integer)*256*256*256) +
	(($toks[2] cast as xs:integer)*256*256) +
	(($toks[3] cast as xs:integer)*256) +
	(($toks[4] cast as xs:integer)) 
	) cast as xs:string 
 } ;


declare variable $logfilename as xs:string external;



<kml xmlns="http://www.opengis.net/kml/2.2">
<Document>
 {
  for $res in ferris-doc( $logfilename )/*
	let $ip := data($res/@ip)
	let $ipstart := libferris:inet_aton( $ip )
	let $ipinfo := ferris-doc-quiet( concat("/tmp/import/ipinfo.db/", $ipstart ))
	where compare($ipinfo/@city,"") != 0
       return
    <Placemark> 
	<name>{ $ip }</name>
	<description>
		A hit from {data($ipinfo/@city)}
		<br/>
			{ $ip }
		<br/>
			At: { data($res/@date) }
		<br/>
			user-agent: { data($res/@user-agent) }
		<br/>
		{
			let $r := data($res/@referer)
			where $r != "-"
			return <a href="{ $r }">{ $r }</a>
		}
	</description>
	<Point>
		<coordinates>
			{concat(data($ipinfo/@long),",",data($ipinfo/@lat))}
		</coordinates>
	</Point>
    </Placemark>
 }
</Document>
</kml> 
